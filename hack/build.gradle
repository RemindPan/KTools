import com.jiangkang.gradle.kotlin.Versions
import com.jiangkang.gradle.kotlin.Deps

def isBuildAsApp = false


if (isBuildAsApp.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion Versions.compileSdkVersion

    defaultConfig {
        minSdkVersion Versions.minSdkVersion
        targetSdkVersion Versions.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        if (isBuildAsApp.toBoolean()) {
            applicationId 'com.jiangkang.hack'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if(isBuildAsApp){
                manifest.srcFile 'src/main/manifest/app/AndroidManifest.xml'
            }else {
                manifest.srcFile 'src/main/manifest/lib/AndroidManifest.xml'
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation Deps.Support.v7
    testImplementation Deps.junit
    androidTestImplementation Deps.Support.Test.runner
    androidTestImplementation Deps.Support.Test.espresso

    implementation Deps.ButterKnife.butterknife
    kapt Deps.ButterKnife.compiler

    implementation project(path: ':tools')

    implementation Deps.ARouter.api
    kapt Deps.ARouter.compiler
    implementation Deps.kotlin

    lintChecks project(path: ':klint')
}

repositories {
    mavenCentral()
}
